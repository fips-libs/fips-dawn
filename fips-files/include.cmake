if (USE_DAWN_SDK)
    get_filename_component(DAWN_SDK_DIR ${FIPS_ROOT_DIR}/../fips-sdks/dawn/dawn ABSOLUTE)
    set(DAWN_BUILD_DIR_DEBUG ${DAWN_SDK_DIR}/out/Debug)
    if (FIPS_MSVC)
        set(DAWN_WEBGPU_DLL_DIR_DEBUG ${DAWN_BUILD_DIR_DEBUG}/Debug)
        set(DAWN_WEBGPU_LIBSTUB_DIR_DEBUG ${DAWN_BUILD_DIR_DEBUG}/src/dawn/native/Debug)
    else()
        set(DAWN_WEBGPU_DLL_DIR_DEBUG ${DAWN_BUILD_DIR_DEBUG}/src/dawn/native)
    endif()
    set(DAWN_BUILD_DIR_RELEASE ${DAWN_SDK_DIR}/out/Release)
    if (FIPS_MSVC)
        set(DAWN_WEBGPU_DLL_DIR_RELEASE ${DAWN_BUILD_DIR_RELEASE}/Release)
        set(DAWN_WEBGPU_LIBSTUB_DIR_RELEASE ${DAWN_BUILD_DIR_RELEASE}/src/dawn/native/Release)
    else()
        set(DAWN_WEBGPU_DLL_DIR_RELEASE ${DAWN_BUILD_DIR_RELEASE}/src/dawn/native)
    endif()
    if (CMAKE_BUILD_TYPE MATCHES "Debug")
        set(DAWN_BUILD_DIR ${DAWN_BUILD_DIR_DEBUG})
        set(DAWN_WEBGPU_DLL_DIR ${DAWN_WEBGPU_DLL_DIR_DEBUG})
        set(DAWN_WEBGPU_LIBSTUB_DIR ${DAWN_WEBGPU_LIBSTUB_DIR_DEBUG})
    else()
        set(DAWN_BUILD_DIR ${DAWN_BUILD_DIR_RELEASE})
        set(DAWN_WEBGPU_DLL_DIR ${DAWN_WEBGPU_DLL_DIR_RELEASE})
        set(DAWN_WEBGPU_LIBSTUB_DIR ${DAWN_WEBGPU_LIBSTUB_DIR_RELEASE})
    endif()
    if (FIPS_MSVC)
        set(DAWN_LIB_PREFIX "")
        set(DAWN_LIB_SUFFIX ".lib")
        set(DAWN_SHARED_SUFFIX ".dll")
    elseif(FIPS_MACOS)
        set(DAWN_LIB_PREFIX "lib")
        set(DAWN_LIB_SUFFIX ".a")
        set(DAWN_SHARED_SUFFIX ".dylib")
    else()
        set(DAWN_LIB_PREFIX "lib")
        set(DAWN_LIB_SUFFIX ".a")
        set(DAWN_SHARED_SUFFIX ".so")
    endif()

    add_library(webgpu_dawn SHARED IMPORTED)
    if (FIPS_MSVC)
        set_target_properties(webgpu_dawn PROPERTIES IMPORTED_LOCATION_DEBUG "${DAWN_WEBGPU_DLL_DIR_DEBUG}/webgpu_dawn.dll")
        set_target_properties(webgpu_dawn PROPERTIES IMPORTED_LOCATION_RELEASE "${DAWN_WEBGPU_DLL_DIR_RELEASE}/webgpu_dawn.dll")
        set_target_properties(webgpu_dawn PROPERTIES IMPORTED_IMPLIB_DEBUG "${DAWN_WEBGPU_LIBSTUB_DIR_DEBUG}/webgpu_dawn.lib")
        set_target_properties(webgpu_dawn PROPERTIES IMPORTED_IMPLIB_RELEASE "${DAWN_WEBGPU_LIBSTUB_DIR_RELEASE}/webgpu_dawn.lib")
    else()
        set_target_properties(webgpu_dawn PROPERTIES IMPORTED_LOCATION "${DAWN_WEBGPU_DLL_DIR}/${DAWN_LIB_PREFIX}webgpu_dawn${DAWN_SHARED_SUFFIX}")
    endif()
    target_include_directories(webgpu_dawn INTERFACE "${DAWN_SDK_DIR}/include" "${DAWN_BUILD_DIR}/gen/include")
endif()
