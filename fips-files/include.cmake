if (USE_DAWN_SDK)
    get_filename_component(DAWN_SDK_DIR ${FIPS_ROOT_DIR}/../fips-sdks/dawn/dawn ABSOLUTE)
    if (CMAKE_BUILD_TYPE MATCHES "Debug")
        set(DAWN_BUILD_DIR ${DAWN_SDK_DIR}/out/Debug)
    else()
        set(DAWN_BUILD_DIR ${DAWN_SDK_DIR}/out/Release)
    endif()
    if (FIPS_MSVC)
        set(DAWN_LIB_PREFIX "")
        set(DAWN_LIB_SUFFIX ".lib")
        set(DAWN_SHARED_SUFFIX ".dll")
    elseif(FIPS_MACOS)
        set(DAWN_LIB_PREFIX "lib")
        set(DAWN_LIB_SUFFIX ".a")
        set(DAWN_SHARED_SUFFIX ".dylib")
    else()
        set(DAWN_LIB_PREFIX "lib")
        set(DAWN_LIB_SUFFIX ".a")
        set(DAWN_SHARED_SUFFIX ".so")
    endif()

    add_library(webgpu_dawn SHARED IMPORTED)
    set_target_properties(webgpu_dawn PROPERTIES IMPORTED_LOCATION "${DAWN_BUILD_DIR}/src/dawn/native/${DAWN_LIB_PREFIX}webgpu_dawn${DAWN_SHARED_SUFFIX}")
    target_include_directories(webgpu_dawn INTERFACE "${DAWN_SDK_DIR}/include" "${DAWN_BUILD_DIR}/gen/include")

    add_library(dawn_glfw STATIC IMPORTED)
    set_target_properties(dawn_glfw PROPERTIES IMPORTED_LOCATION "${DAWN_BUILD_DIR}/src/dawn/glfw/${DAWN_LIB_PREFIX}dawn_glfw${DAWN_LIB_SUFFIX}")
    target_link_libraries(dawn_glfw INTERFACE glfw3)

    add_library(glfw3 STATIC IMPORTED)
    set_target_properties(glfw3 PROPERTIES IMPORTED_LOCATION "${DAWN_BUILD_DIR}/third_party/glfw/src/${DAWN_LIB_PREFIX}glfw3${DAWN_LIB_SUFFIX}")
    if (FIPS_MACOS)
        target_link_libraries(glfw3 INTERFACE "-framework Cocoa" "-framework IOKit" "-framework QuartzCore")
    endif()
    add_library(webgpu_glfw ALIAS dawn_glfw)
endif()
